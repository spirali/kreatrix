/*
   struct.kx
   Copyright (C) 2007, 2008  Stanislav Bohm

   This file is part of Kreatrix.

   Kreatrix is free software; you can redistribute it and/or modify it under
   the terms of the GNU General Public License as published by the Free
   Software Foundation; either version 2, or (at your option) any later
   version.

   Kreatrix is distributed in the hope that it will be useful, 
   but WITHOUT ANY WARRANTY; without even the implied warranty 
   of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the 
   GNU General Public License for more details.

   You should have received a copy of the GNU General Public License
   along with Kreatrix; see the file COPYING. If not, see 
   <http://www.gnu.org/licenses/>.
*/
	


GStruct = GWrapper clone do: {

	Attribute = Object clone do: {
		type = "GStructAttribute".

		name <- "".
		gtype <- nil.

		wrapper <- nil.

		slotNameReader = { 
			^(name asListBy: "_") asStringCamelCase.
		}.

		slotNameWriter = {
			^slotNameReader,":".
		}.

		functionsPrefix = { ^ wrapper functionsPrefix }.

		functionReader = { |func|
				func << GRawFunction clone.
				func slotName: slotNameReader.
				func wrapperCName: functionsPrefix,"_", name.

				// TODO: check if data is not NULL
				func codeString: ("\treturn KX",gtype name asUppercase,
								  "( ((", wrapper cname,") self->data.ptr)->",name,");").
				^func.
		}.

		functionWriter = { |func|
				func << GRawFunction clone.
				func numberOfParameters: 1.
				func slotName: slotNameWriter.
				func wrapperCName: functionsPrefix,"_", name,"_".
				// TODO: check if data is not NULL

				func codeString: (gtype codeForGettingParamId: 0),
					"\n\t((", wrapper cname,") self->data.ptr)->",name," = param0;",
					"\n\tKXRETURN(self);".
				^func.
		}


	}.
	
	type = "GStruct".

	attributes <- #().

	options <- "".

	ownInitCode <- nil.

	isStruct = true.

	emptyPrototype <- false.

	init = {
		resend init.
		attributes: #().
	}.

	loadBodyFromString: = { :aString | 
		|list|
		list << (aString sliceBetween: "{" and: "}") asListBy: ";".
		(list foreach: [ :str |  |list attr|
			str << str trim.
			(str beginsWith: "$") ifTrue: [ 
				options: (str sliceBetween: "$" and: "$") trim.
			] ifFalse: [ 
				str isEmpty ifFalse: [ 
					list << (str asListBy: " ").
					attr << Attribute clone.
					attr wrapper: self.
					attr name: list last trim.
					attr gtype: (generator findType: (list first trim)).

					attributes add: attr.
				]
			].
		]).

		options isEmpty ifFalse: [
			(options == "empty_prototype") ifTrue: [
				ownInitCode: "NULL".
				emptyPrototype: true.
			] ifFalse: [
				ownInitCode: options.
			].
		].
	}.
	
	loadFromStringType1: = { :aString |
		name: (aString sliceBetween: "}" and: ";") trim.
		loadBodyFromString: aString.
		^true.
	}.

	loadFromFileType1:generator: = { :filename :generator|
		|file list|
		list << #().
		file << File clone name: filename.
		file openForReading.
		(file contentAsString asListSeparator: "typedef") foreach: [ :str |
				|struct str2|
				str2 << str trim.
				str2 isEmpty ifFalse: [
					struct << GStruct clone.
					struct generator: generator.
					(struct loadFromStringType1: str trim) ifTrue: [ list add: struct ].
				].
		].
		^list.
	}.

	initPrototypeCode = { |str|
		  str << ownInitCode isNil ifFalse: [ 
			  "self->data.ptr = ",ownInitCode,";\n".
		  ] ifTrue: [
			  name, " p;\n",
			  "\tmemset(&p, 0, sizeof(",name,"));\n",
			  "\tself->data.ptr = g_boxed_copy(", typeCName, ",&p);\n".
		  ].
		  ^str, "\tkxgtk_register_extension(", typeCName, ", &", extensionName, ");\n".
	}.

	cloneFunction = {
		|func|
		func << GRawFunction clone.
		func slotName: "clone".
		func wrapperCName: functionsPrefix,"_clone".
		emptyPrototype ifTrue: [
			func codeString: ("\tKxObject *clone = kxobject_raw_clone(self);\n",
							  "\tif(self->data.ptr) {\n",
							  "\t\tclone->data.ptr = g_boxed_copy(",typeCName,", self->data.ptr);\n",
							  "\t}\n",
							  "\treturn clone;"
							  ). 
			] ifFalse: [
				func codeString: ("\tKxObject *clone = kxobject_raw_clone(self);\n",
								  "\tclone->data.ptr = g_boxed_copy(",typeCName,", self->data.ptr);\n",
								  "\treturn clone;"
								  ). 
			].
		^func
	}.

	freeCode = { 
		^emptyPrototype ifTrue: [ 
			"if (self->data.ptr) g_boxed_free(",typeCName,",self->data.ptr);"
		] ifFalse: [
			"g_boxed_free(",typeCName,",self->data.ptr);"
		].
	}.

	newFromCode = {
	^"void *boxed = g_boxed_copy(",typeCName,",data);\n",
"""
	KxObject *self; 
	KxObject *proto = kxcore_get_prototype(core, &kx""",name asLowercase,"""_extension);
	self = kxobject_raw_clone(proto);
	self->data.ptr = boxed;
	return self;
""" }.

	initFunctions = {
		functions: #(cloneFunction).
		attributes foreach: [ :attr | 
			functions add: attr functionReader.
			functions add: attr functionWriter.
		].
	}.

}.
